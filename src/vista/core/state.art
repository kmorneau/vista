; Vista UI - State Management and Face Registration

vista_state: #[
        face_counter: 0
        last_faces: #[]
        last_root: null
        face_names: #[]
        last_bindings: #[]
        webview_open: false
        layout_pending_attrs: #[]
        layout_scope_stack: @[]
        tabs_id_counter: 0
]

next_face_id: function [] [
        vista_state\face_counter: vista_state\face_counter + 1
        "face-" ++ to :string vista_state\face_counter
]

register_face: function [faces parentId type attrs html] [
        id: next_face_id
        face: #[
                id: id
                type: type
                attrs: attrs
                html: html
                children: []
        ]
        faces\[id]: face
        if notEqual? parentId null [
                parent: faces\[parentId]
                if not? key? parent "children" [
                        parent\children: []
                ]
                parent\children: parent\children ++ @[id]
                faces\[parentId]: parent
        ]
        id
]

current_parent_id: function [stack] [
        len: size stack
        if equal? len 0 [
                return null
        ]
        get stack (len - 1)
]

register_face_name: function [face] [
        if key? face\attrs "id" [
                nameStr: to :string face\attrs\id
                vista_state\face_names\[nameStr]: face\id
        ]
]

set_face_attrs: function [faces id attrs] [
        if key? faces id [
                face: faces\[id]
                face\attrs: attrs
                faces\[id]: face
        ]
]

set_face_attrs_and_name: function [faces id attrs] [
        set_face_attrs faces id attrs
        if key? faces id [
                face: faces\[id]
                register_face_name face
        ]
]

set_face_html: function [faces id html] [
        if key? faces id [
                face: faces\[id]
                face\html: html
                faces\[id]: face
        ]
]

set_face_meta: function [faces id meta] [
        if key? faces id [
                face: faces\[id]
                face\meta: meta
                faces\[id]: face
        ]
]

with_face_id: function [attrs id] [
        attrs\["data-face-id"]: id
        attrs
]

merge_faces: function [dest src] [
        keysList: keys src
        i: 0
        keys_len: size keysList
        while [less? i keys_len][
                k: get keysList i
                dest\[k]: src\[k]
                i: i + 1
        ]
]

append_children_from_root: function [faces parentId rootId] [
        if any? @[equal? parentId null equal? rootId null] [
                return
        ]
        rootFace: faces\[rootId]
        if key? rootFace "children" [
                kids: rootFace\children
                klen: size kids
                i: 0
                while [less? i klen][
                        parent: faces\[parentId]
                        if not? key? parent "children" [
                                parent\children: []
                        ]
                        parent\children: parent\children ++ @[get kids i]
                        faces\[parentId]: parent
                        i: i + 1
                ]
        ]
]
