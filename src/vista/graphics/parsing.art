; Vista Graphics Parsing

; Normalize draw blocks so `fill-pen` and `line-width` survive Arturo tokenization
vg_normalize_block: function [block] [
    if notEqual? (type block) :block [
        return block
    ]
    out: []
    i: 0
    blen: size block
    while [less? i blen] [
        tok: get block i
        tok_type: type tok
        did_combine: false
        if any? @[equal? tok_type :word equal? tok_type :attributeLabel] [
            combinedStr: to :string tok
            j: i
            while [less? (j + 2) blen] [
                mid: get block (j + 1)
                tail: get block (j + 2)
                if not? all? @[equal? (type mid) :symbol equal? (to :string mid) "-" any? @[equal? (type tail) :word equal? (type tail) :label]] [
                    break
                ]
                combinedStr: combinedStr ++ "-" ++ (to :string tail)
                j: j + 2
            ]
            if greater? j i [
                switch equal? tok_type :attributeLabel [
                    out: out ++ @[to :attributeLabel combinedStr]
                ][
                    out: out ++ @[to :word combinedStr]
                ]
                i: j + 1
                did_combine: true
            ]
        ]
        if not? did_combine [
            out: out ++ @[tok]
            i: i + 1
        ]
    ]
    out
]
